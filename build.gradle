plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.8'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    mavenLocal()
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def grpcVer = '1.30.2'

dependencies {
    implementation group: 'io.grpc', name: 'grpc-core', version: "${grpcVer}"
    implementation group: 'io.grpc', name: 'grpc-alts', version: "${grpcVer}"
    implementation group: 'org.apache.tomcat', name: 'annotations-api', version: '6.0.53'
    runtime group: 'io.grpc', name: 'grpc-grpclb', version: "${grpcVer}"
    runtime group: 'com.google.cloud', name: 'google-cloud-logging-logback', version: '0.80.0-alpha'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.12.0" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVer}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

application {
    mainClassName = "com.on.DirectPathRlsTest"
}
